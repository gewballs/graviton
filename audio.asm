
            ;# ===============================================================//
            ;#                                                                //
            ;# ===============================================================//

;#Name $0300 audioSize
;#Data l audio {
    $C0 $20 $8F $6C $F2 $8F $E0 $F3 $E8 $00 $5D $D8 $F2 $C4 $F3 $3D
    $C8 $6C $F0 $FB $C8 $80 $90 $F3 $C4 $F1 $C4 $F4 $C4 $F5 $C4 $F6
    $C4 $F7 $C4 $FA $C4 $FB $C4 $FC $8F $00 $00 $8F $05 $01 $FD $D7
    $00 $FC $D0 $FB $AB $01 $D0 $F7 $5D $40 $AF $C8 $00 $D0 $FB $20
    $AF $C8 $F0 $90 $FB $CD $02 $C9 $FF $FF $CD $FF $BD $5D $60 $E0

    $8F $07 $F1 $8F $01 $20 $3F $66 $02 $3F $67 $02 $3F $74 $02 $3F
    $C3 $03 $AB $10 $2F $F0 $6F $E4 $FD $C4 $11 $E4 $FE $C4 $12 $E4
    $FF $C4 $13 $6F $78 $01 $20 $D0 $06 $3F $8E $02 $8F $00 $20 $CD
    $70 $3F $A8 $02 $7D $60 $88 $10 $68 $F0 $5D $90 $F4 $6F $CD $78
    $8D $00 $F6 $F0 $03 $D4 $00 $F6 $F1 $03 $D4 $01 $FC $FC $7D $60
    $88 $10 $68 $F8 $5D $90 $EB $6F $F4 $02 $D8 $00 $5D $F4 $11 $F8
    $00 $60 $94 $00 $74 $01 $90 $11 $F4 $03 $1C $60 $88 $08 $84 $00
    $3F $CC $02 $F8 $00 $B0 $F1 $E8 $00 $D4 $00 $6F $5D $3F $BA $03
    $68 $02 $D0 $05 $3F $1B $03 $2F $3D $68 $01 $D0 $05 $3F $3F $03
    $2F $37 $68 $00 $D0 $05 $3F $47 $03 $2F $2E $68 $03 $D0 $05 $3F
    $48 $03 $2F $22 $68 $04 $D0 $05 $3F $53 $03 $2F $19 $68 $05 $D0
    $05 $3F $5F $03 $2F $10 $68 $06 $D0 $05 $3F $8B $03 $2F $07 $68
    $07 $D0 $06 $3F $AF $03 $80 $2F $01 $60 $6F $3F $BA $03 $2D $3F
    $BA $03 $F8 $00 $D4 $02 $8D $00 $DB $00 $5D $FD $E8 $01 $DC $30
    $03 $1C $2F $FA $48 $07 $C4 $F1 $AE $D4 $FA $8F $07 $F1 $6F $3F
    $BA $03 $F8 $00 $D4 $01 $6F $6F $3F $BA $03 $C4 $F2 $3F $BA $03
    $C4 $F3 $6F $3F $BA $03 $FD $3F $BA $03 $D4 $01 $DB $00 $6F $3F
    $BA $03 $2D $D8 $01 $F8 $00 $7D $60 $88 $04 $94 $03 $5D $AE $74
    $00 $F0 $0B $30 $09 $BB $00 $F8 $01 $3F $53 $03 $2F $0C $E8 $00
    $D4 $00 $F8 $01 $3F $BA $03 $3F $BA $03 $6F $D8 $01 $F8 $00 $F4
    $03 $68 $03 $B0 $11 $BB $03 $F8 $01 $3F $BA $03 $FD $3F $BA $03
    $D4 $03 $DB $02 $2F $08 $F8 $01 $3F $BA $03 $3F $BA $03 $6F $F8
    $00 $9B $03 $10 $04 $E8 $00 $D4 $03 $6F $E7 $00 $BB $00 $D0 $02
    $BB $01 $6F $6F 
                    $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
    $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
    $01 $FF $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $04 $E0 $03
    $62 $04 $E0 $03 $E0 $03 $E0 $03 $E0 $03 $E0 $03 $E0 $03 $E0 $03

    $0408 $0408
    $0411 $0411
    
    $03 $00 $00 $00 $00 $00 $00 $00 $00 

    $C2 $00 $11 $12 $22 $33 $33 $44 $45 
    $C2 $55 $56 $66 $66 $67 $77 $77 $77
    $C2 $77 $77 $77 $77 $77 $77 $77 $77
    $C2 $66 $66 $66 $55 $55 $44 $43 $33
    $B2 $55 $43 $32 $11 $0F $FE $DD $CB
    $C2 $DD $DD $CC $CB $BB $BA $AA $AA
    $C2 $A9 $99 $99 $99 $99 $99 $99 $99
    $C2 $99 $99 $99 $99 $AA $AA $AA $BB
    $C3 $BB $CC $CD $DD $DE $EE $FF $F0

    $03 $5D $04
    $03 $6D $7F  $03 $7D $0F
    $03 $0C $7F  $03 $1C $7F
    $03 $6C $20

    $03 $00 $7F  $03 $01 $7F
    $03 $02 $00
    $03 $04 $01
    $03 $05 $C6  $03 $06 $D0  $03 $07 $00

    $02 $30 $00
    
    $03 $03 $36  $03 $4C $01  $01 $20
    
    $03 $5C $01 $01 $02

    $03 $03 $36  $03 $4C $01  $01 $40
    $03 $03 $36  $03 $4C $01  $01 $40
    $03 $03 $2E  $03 $4C $01  $01 $20
    $03 $03 $36  $03 $4C $01  $01 $40
    $03 $03 $3C  $03 $4C $01  $01 $80
    $03 $03 $24  $03 $4C $01  $01 $80
    $04 $048C

                                                                $00
    $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
    $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
    $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
    $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
}

;#{
Sound 0: Silence
Sound 1: A 440Hz Sine Wave
PH=10 12 14 16 18 1A 1C 1E 20 22 24 26 28 2A 2C 2E 30 32 34 36 38 3A 3C 3E
No=A  A# B  C  C# D  D# E  F  F# G  G# A  A# B  C  C# D  D# E  F  F# G  G#
;#}


;#{

TODO: BGM Mute, Stop, Bgm.New clears everything

pass 00
wait 01 ww
time 02 nn tt
data 03 rr dd
jump 04 aaaa
loop 05 ll aaaa
call 06 aaaa
back 07

bgm instructions
  *play
  *pause
  *song ss
  *load aaaa ssss bbbbbbb
  *volume ll rr
  *time nn tt

sfx instructions
  sfx ss ll rr

Name 00 bytes temporary[0x10]

Name 10 cycles
Name 11 counter
Name 11 counter0
Name 12 counter1
Name 13 counter2

Name 20 bgm state

Name 70 byte voice[0].time
Name 71 byte voice[0].wait
Name 72 byte voice[0].timer
Name 73 byte voice[0].depth
Name 74 byte voice[0].loop[n]
Name 78 word voice[0].code[n]


              ;Reset:
0200          ;        :                       | // Processor
0200 C0       ;        :DI                     | Di();
0201 20       ;        :CLRP                   | Clrp();
0202          ;        :                       | // Flg
0202 8F 6C F2 ;        :MOV spc.base,#DSP_flg  | spc.base=DSP_flg;
0205 8F E0 F3 ;        :MOV spc.data,#$E0      | spc.data=0xE0;
0208          ;        :                       | // Dsp
0208 E8 00    ;        :MOV A,#$00             | for(X=0;X<0x80;X++){
020A 5D       ;        :MOV X,A                |
020B D8 F2    ;-       :MOV spc.base,X         |   spc.base=X;
020D C4 F3    ;        :MOV spc.data,A         |   spc.data=0;
020F 3D       ;-Flg    :INC X                  |
0210 C8 6C    ;        :CMP X,#$6C             |   if(X+1==DSP_flg) X++;
0212 F0 FB    ;        :BEQ -Flg               |
0214 C8 80    ;        :CMP X,#$80             |
0216 90 F3    ;        :BCC -                  | }
0218          ;        :                       | // Spc
0218 C4 F1    ;        :MOV spc.control,A      | spc.control=0;
021A C4 F4    ;        :MOV spc.port0,A        | spc.port0  =0;
021C C4 F5    ;        :MOV spc.port1,A        | spc.port1  =0;
021E C4 F6    ;        :MOV spc.port2,A        | spc.port2  =0;
0220 C4 F7    ;        :MOV spc.port3,A        | spc.port3  =0;
0222 C4 FA    ;        :MOV spc.timer0,A       | spc.timer0 =0;
0224 C4 FB    ;        :MOV spc.timer1,A       | spc.timer1 =0;
0226 C4 FC    ;        :MOV spc.timer2,A       | spc.timer2 =0;
0228          ;        :                       | // Ram
0228 8F 00 00 ;        :MOV $00,#$00           | $00=0x0500; // End of code
022B 8F 05 01 ;        :MOV $01,#$05           |
022E FD       ;        :MOV Y,A                | Y=0;
022F D7 00    ;-       :MOV [$00]+Y,A          | do{$00[Y]=0;}while(++Y||++$01);
0231 FC       ;        :INC Y                  |
0232 D0 FB    ;        :BNE -                  |
0234 AB 01    ;        :INC $01                |
0236 D0 F7    ;        :BNE -                  |
0238          ;        :                       | // Page 1
0238 5D       ;        :MOV X,A                | X=0;
0239 40       ;        :SETP                   | do{$0100[X]=0;}while(++X);
023A AF       ;-       :MOV (X)+,A             |
023B C8 00    ;        :CMP X,#$00             |
023D D0 FB    ;        :BNE -                  |
023F          ;        :                       | // Page 0
023F 20       ;        :CLRP                   | do{*X=0;}while(++X<0xF0);
0240 AF       ;-       :MOV (X)+,A             |
0241 C8 F0    ;        :CMP X,#$F0             |
0243 90 FB    ;        :BCC -                  |
0245          ;        :                       | // Vector
0245 CD 02    ;        :MOV X,#$02             | $FFFE=Reset;
0247 C9 FF FF ;        :MOV $FFFF,X            |
024A          ;        :                       | // CPU
024A CD FF    ;        :MOV X,#$FF             | SP=0xFF;
024C BD       ;        :MOV SP,X               |
024D 5D       ;        :MOV X,A                | X=0;
024E 60       ;        :CLRC                   | Clrc();
024F E0       ;        :CLRV                   | Clrv();

              ;Main:
0250 8F 07 F1 ;        :MOV spc.control,#$07   | spc.control=0x07;
0253 8F 01 20 ;        :MOV bgm.state,#$01     | bgm.state=0x01;
0256          ;        :                       | for(;;){
0256 3F 66 02 ;-       :CALL Sync              |   Sync();
0259 3F 67 02 ;        :CALL Time              |   Time();
025C 3F 74 02 ;        :CALL Bgm               |   Bgm();
025F 3F C3 03 ;        :CALL Sfx               |   Sfx();
0262 AB 10    ;        :INC cycles             |   cycles++;
0264 2F F0    ;        :BRA -                  | }

              ;Sync:
0266          ;        :                       |
0266 6F       ;        :RET                    | return;

              ;Time:
0267 E4 FD    ;        :MOV A,spc.counter0     | counter0=spc.counter0;
0269 C4 11    ;        :MOV counter0,A         |
026B E4 FE    ;        :MOV A,spc.counter1     | counter1=spc.counter1;
026D C4 12    ;        :MOV counter1,A         |
026F E4 FF    ;        :MOV A,spc.counter2     | counter2=spc.counter2;
0271 C4 13    ;        :MOV counter2,A         |
0273          ;        :                       |
0273 6F       ;        :RET                    | return;

              ;Bgm:
0274 78 01 20 ;        :CMP bgm.state,#$01     | if(bgm.state==0x01){
0277 D0 06    ;        :BNE +                  |
0279 3F 8E 02 ;        :CALL Bgm.New           |   Bgm.New();
027C 8F 00 20 ;        :MOV bgm.state,#$00     |   bgm.state=0x00;
027F          ;+       :                       | }
027F CD 70    ;        :MOV X,#$70             | for(X=0x70;X<0xF0;X+=0x10){
0281 3F A8 02 ;-       :CALL Track             |   Track(X);
0284 7D       ;        :MOV A,X                |
0285 60       ;        :CLRC                   |
0286 88 10    ;        :ADC A,#$10             |
0288 68 F0    ;        :CMP A,#$F0             |
028A 5D       ;        :MOV X,A                |
028B 90 F4    ;        :BCC -                  | }
028D          ;        :                       |
028D 6F       ;        :RET                    | return;

              ;Bgm.New:
028E CD 78    ;        :MOV X,#$78             | for(X=0x78;Y=0;X<0xF8;X+=0x10){
0290 8D 00    ;        :MOV Y,#$00             |
0292 F6 F0 03 ;-       :MOV A,$03F0+Y          |   X[0]=$03F0[Y];
0295 D4 00    ;        :MOV $00+X,A            |
0297 F6 F1 03 ;        :MOV A,$03F1+Y          |   X[1]=$03F1[Y];
029A D4 01    ;        :MOV $01+X,A            |
029C FC       ;        :INC Y                  |
029D FC       ;        :INC Y                  |
029E 7D       ;        :MOV A,X                |
029F 60       ;        :CLRC                   |
02A0 88 10    ;        :ADC A,#$10             |
02A2 68 F8    ;        :CMP A,#$F8             |
02A4 5D       ;        :MOV X,A                |
02A5 90 EB    ;        :BCC -                  | }
02A7          ;        :                       |
02A7 6F       ;        :RET                    | return;

              ;Track:
02A8 F4 02    ;        :MOV A,voice.timer+X    | A=counter[voice[X].timer];
02AA D8 00    ;        :MOV $00,X              |
02AC 5D       ;        :MOV X,A                |
02AD F4 11    ;        :MOV A,counter+X        |
02AF F8 00    ;        :MOV X,$00              |
02B1 60       ;        :CLRC                   | A+=voice[X].time;
02B2 94 00    ;        :ADC A,voice.time+X     |
02B4 74 01    ;        :CMP A,voice.wait+X     | if(A>=voice[X].wait){
02B6 90 11    ;        :BCC +Wait              |
02B8          ;        :                       |
02B8 F4 03    ;-       :MOV A,voice.depth+X    |   do{
02BA 1C       ;        :ASL A                  |     A=X+0x08+voice[X].depth*2;
02BB 60       ;        :CLRC                   |
02BC 88 08    ;        :ADC A,#$08             |
02BE 84 00    ;        :ADC A,$00              |
02C0 3F CC 02 ;        :CALL Parse             |   }while(Parse(A));
02C3 F8 00    ;        :MOV X,$00              |
02C5 B0 F1    ;        :BCS -                  |
02C7          ;        :                       |
02C7 E8 00    ;        :MOV A,#$00             |   A=0x00;
02C9          ;        :                       | }
02C9 D4 00    ;+Wait   :MOV voice.time+X,A     | voice[X].time=A;
02CB          ;        :                       |
02CB 6F       ;        :RET                    | return;

              ;Parse:
02CC 5D       ;        :MOV X,A                | X=A;
02CD 3F BA 03 ;        ;CALL Get               | A=Get();
02D0 68 02    ;        :CMP A,#$02             | if(A==0x02);
02D2 D0 05    ;        :BNE +                  |
02D4 3F 1B 03 ;        :CALL Time              |   Time();
02D7 2F 3D    ;        :BRA +Next              |
02D9 68 01    ;+       :CMP A,#$01             | }else if(A==0x01){
02DB D0 05    ;        :BNE +                  |
02DD 3F 3F 03 ;        :CALL Wait              |   Wait(); ok
02E0 2F 37    ;        :BRA +End               |
02E2 68 00    ;+       :CMP A,#$00             | }else if(A==0x00){
02E4 D0 05    ;        :BNE +                  |
02E6 3F 47 03 ;        :CALL Pass              |   Pass(); ok
02E9 2F 2E    ;        :BRA +End               |
02EB 68 03    ;+       :CMP A,#$03             | }else if(A==0x03){
02ED D0 05    ;        :BNE +                  |
02EF 3F 48 03 ;        :CALL Data              |   Data(); ok
02F2 2F 22    ;        :BRA +Next              |
02F4 68 04    ;+       :CMP A,#$04             | }else if(A==0x04){
02F6 D0 05    ;        :BNE +                  |
02F8 3F 53 03 ;        :CALL Jump              |   Jump(); ok
02FB 2F 19    ;        :BRA +Next              |
02FD 68 05    ;+       :CMP A,#$05             | }else if(A==0x05){
02FF D0 05    ;        :BNE +                  |
0301 3F 5F 03 ;        :CALL Loop              |   Loop(); ok
0304 2F 10    ;        :BRA +Next              |
0306 68 06    ;+       :CMP A,#$06             | }else if(A==0x06){
0308 D0 05    ;        :BNE +                  |
030A 3F 8B 03 ;        :CALL Call              |   Call(); ok
030D 2F 07    ;        :BRA +Next              |
030F 68 07    ;+       :CMP A,#$07             | }else if(A==0x07){
0311 D0 06    ;        :BNE +End               |
0313 3F AF 03 ;        :CALL Back              |   Back(); ok
0316 80       ;+Next   :SETC                   |
0317 2F 01    ;        :BRA +Return            |
0319 60       ;+End    :CLRC                   | }
031A 6F       ;+Return :RET                    | return;

              ;Time:
031B 3F BA 03 ;        :CALL Get               | A=Get(X);
031E 2D       ;        :PUSH A                 | Push(A);
031F 3F BA 03 ;        :CALL Get               | A=Get(X);
0322          ;        :                       |
0322 F8 00    ;        :MOV X,voice            | voice->timer=A;
0324 D4 02    ;        :MOV timer+X,A          |
0326 8D 00    ;        :MOV Y,#$00             | voice->time=0x00;
0328 DB 00    ;        :MOV time+X,Y           |
032A          ;        :                       |
032A 5D       ;        :MOV X,A                | X=A;
032B FD       ;        :MOV Y,A                | for(Y=A,A=0x01;--Y>0;A<<1);
032C E8 01    ;        :MOV A,#$01             |
032E DC       ;-       :DEC Y                  |
032F 30 03    ;        :BMI +                  |
0331 1C       ;        :ASL A                  |
0332 2F FA    ;        :BRA -                  |
0334 48 07    ;+       :EOR A,#$07             | spc.control=A^0x07;
0336 C4 F1    ;        :MOV spc.control,A      |
0338          ;        :                       |
0338 AE       ;        :POP A                  | spc.timer[X]=Pop();
0339 D4 FA    ;        :MOV spc.timer0+X,A     |
033B          ;        :                       |
033B 8F 07 F1 ;        :MOV spc.control,#$07   | spc.control=0x07;
033E          ;        :                       |
033E 6F       ;        :RET                    |

              ;Wait:
033F 3F BA 03 ;        :CALL Get               | voice->wait=Get(X);
0342 F8 00    ;        :MOV X,voice            |
0344 D4 01    ;        :MOV wait+X,A           |
0346 6F       ;        :RET                    | return;

              ;Pass:
0347 6F       ;        :RET                    | return;

              ;Data:
0348 3F BA 03 ;        :CALL Get               | spc.base=Get(X);
034B C4 F2    ;        :MOV spc.base,A         |
034D 3F BA 03 ;        :CALL Get               | spc.data=Get(X);
0350 C4 F3    ;        :MOV spc.data,A         |
0352 6F       ;        :RET                    | return;

              ;Jump:
0353 3F BA 03 ;        :CALL Get               | Y=Get(X);
0356 FD       ;        :MOV Y,A                |
0357 3F BA 03 ;        :CALL Get               | A=Get(X);
035A D4 01    ;        :MOV $01+X,A            | X[0]=Y;
035C DB 00    ;        :MOV $00+X,Y            | X[1]=A;
035E 6F       ;        :RET                    | return;

              ;Loop:
035F 3F BA 03 ;        :CALL Get               | A=Get(X);
0362 2D       ;        :PUSH A                 |
0363 D8 01    ;        :MOV code,X             | code=X;
0365 F8 00    ;        :MOV X,voice            | X=voice+0x04+voice->depth;
0367 7D       ;        :MOV A,X                |
0368 60       ;        :CLRC                   |
0369 88 04    ;        :ADC A,#$04             |
036B 94 03    ;        :ADC A,depth+X          |
036D 5D       ;        :MOV X,A                |
036E AE       ;        :POP A                  |
036F 74 00    ;        :CMP A,$00+X            | if(A>*X){
0371 F0 0B    ;        :BEQ +NoLoop            |
0373 30 09    ;        :BMI +NoLoop            |
0375 BB 00    ;        :INC $00+X              |   *X++;
0377 F8 01    ;        :MOV X,code             |   X=code;
0379 3F 53 03 ;        :CALL Jump              |   Jump();
037C 2F 0C    ;        :BRA +Return            | }else{
037E E8 00    ;+NoLoop :MOV A,#$00             |   *X=0x00;
0380 D4 00    ;        :MOV $00+X,A            |
0382 F8 01    ;        :MOV X,code             |   X=code;
0384 3F BA 03 ;        :CALL Get               |   Get(X);
0387 3F BA 03 ;        :CALL Get               |   Get(X);
038A          ;        :                       | }
038A 6F       ;+Return :RET                    | return;

              ;Call:
038B D8 01    ;        :MOV code,X             | if(voice->depth<3){
038D F8 00    ;        :MOV X,voice            |
038F F4 03    ;        :MOV A,depth+X          |
0391 68 03    ;        :CMP A,#$03             |
0393 B0 11    ;        :BCS +NoCall            |
0395          ;        :                       |
0395 BB 03    ;        :INC depth+X            |   voice->depth++;
0397 F8 01    ;        :MOV X,code             |
0399 3F BA 03 ;        :CALL Get               |   Y=Get(X);
039C FD       ;        :MOV Y,A                |
039D 3F BA 03 ;        :CALL Get               |   A=Get(X);
03A0 D4 03    ;        :MOV $03+X,A            |   X[0]=Y;
03A2 DB 02    ;        :MOV $02+X,Y            |   X[1]=A;
03A4 2F 08    ;        :BRA +Call              | }else{
03A6          ;        :                       |
03A6 F8 01    ;+NoCall :MOV X,code             |
03A8 3F BA 03 ;        :CALL Get               |   Get(X);
03AB 3F BA 03 ;        :CALL Get               |   Get(X);
03AE          ;+Call   :                       | }
03AE 6F       ;        :RET                    | return;

              ;Back:
03AF F8 00    ;        :MOV X,voice            | if(--voice->depth<0x00){
03B1 9B 03    ;        :DEC depth+X            |
03B3 10 04    ;        :BPL +                  |
03B5 E8 00    ;        :MOV A,#$00             |   voice->depth=0x00;
03B7 D4 03    ;        :MOV depth+X,A          |
03B9          ;+       :                       | }
03B9 6F       ;        :RET                    | return;

              ;Get:
03BA E7 00    ;        :MOV A,[$00+X]          | A=*(*X)++;
03BC BB 00    ;        :INC $00+X              |
03BE D0 02    ;        :BNE +                  |
03C0 BB 01    ;        :INC $01+X              |
03C2          ;+       :                       |
03C2 6F       ;        :RET                    | return;

              ;Sfx:
03C3          ;        :                       |
03C3 6F       ;        :RET                    | return;


>:(
              ;        :                   | //=====================//
              ;        :                   | // SPC700 IPL Boot Rom //
              ;        :                   | // (c) Nintendo        //
              ;        :                   | //=====================//
              ;Ipl:
FFC0 CD EF    ;        :MOV X,#$EF         | SP=0xEF;
FFC2 BD       ;        :MOV SP,X           |
FFC3 E8 00    ;        :MOV A,#$00         | for(X=0xEF;X;X--) *X=0x00;
FFC5 C6       ;-Clear  :MOV (X),A          |
FFC6 1D       ;        :DEC X              |
FFC7 D0 FC    ;        :BNE -Clear         |
FFC9 8F AA F4 ;        :MOV port0,#$AA     | port0=0xAA; // Ready
FFCC 8F BB F5 ;        :MOV port1,#$BB     | port1=0xBB; // Ready
FFCF 78 CC F4 ;-Wait   :CMP port0,#$CC     | while(port0!=0xCC); // Wait
FFD2 D0 FB    ;        :BNE -Wait          | goto +;
FFD4 2F 19    ;        :BRA +Base          | for(;;){
FFD6 EB F4    ;-Move   :MOV Y,port0        |   while(Y=port0); // Wait
FFD8 D0 FC    ;        :BNE -Move          |   for(;;){
FFDA 7E F4    ;-Next   :CMP Y,port0        |     if(Y==port0){ // Data
FFDC D0 0B    ;        :BNE +              |
FFDE E4 F5    ;        :MOV A,port1        |       A=port1; // Read
FFE0 CB F4    ;        :MOV port0,Y        |       port0=Y; // Echo
FFE2 D7 00    ;        :MOV [$00]+Y,A      |       $00[Y]=A; // Write
FFE4 FC       ;        :INC Y              |       if(++Y||++$01>=0) continue;
FFE5 D0 F3    ;        :BNE -Next          |     }else{ // Idle
FFE7 AB 01    ;        :INC $01            |       if(Y>port0) continue;
FFE9 10 EF    ;+       :BPL -Next          |     }
FFEB 7E F4    ;        :CMP Y,port0        |     if(Y<port0) break; // End
FFED 10 EB    ;        :BPL -Next          |   }
FFEF BA F6    ;+Base   :MOVW YA,port2      | +:$00=(word)port2; // Address
FFF1 DA 00    ;        :MOVW $00,YA        |
FFF3 BA F4    ;        :MOVW YA,port0      |   X=port1; // Move/jump flag
FFF5 C4 F4    ;        :MOV port0,A        |   port0=port0; // Echo
FFF7 DD       ;        :MOV A,Y            |
FFF8 5D       ;        :MOV X,A            |   if(!X) break;
FFF9 D0 DB    ;        :BNE -Move          | }
FFFB 1F 00 00 ;        :JMP [$0000+X]      | Jmpix(0x0000);

              ;Vector:
FFFE C0 FF    ;        :Ipl                | // IPL Rom

;#}

            ;# ===============================================================//
            ;#                                                                //
            ;#                                                                //
            ;#                                                                //
            ;# ===============================================================//

