
            ;# ===============================================================//
            ;# Graviton                                                       //
            ;#                                                                //
            ;# SPC700 Audio Driver                                            //
            ;# ===============================================================//

            ;# ===============================================================//
            ;#                                                                //
            ;# Image                                                          //
            ;#                                                                //
            ;# ===============================================================//
            ;#Name $6000 audioSize
            ;#Data l audio{
              $C0 $20 $8F $6C $F2 $8F $E0 $F3 $E8 $00 $5D $D8 $F2 $C4 $F3 $3D
              $C8 $6C $F0 $FB $C8 $80 $90 $F3 $C4 $F1 $C4 $F4 $C4 $F5 $C4 $F6
              $C4 $F7 $C4 $FA $C4 $FB $C4 $FC $8F $00 $00 $8F $60 $01 $FD $D7
              $00 $FC $D0 $FB $AB $01 $D0 $F7 $5D $40 $AF $C8 $00 $D0 $FB $20

              $AF $C8 $F0 $90 $FB $CD $02 $C9 $FF $FF $CD $FF $BD $5D $60 $E0
              $8F $07 $F1 $8F $01 $20 $3F $66 $02 $3F $67 $02 $3F $74 $02 $3F
              $75 $02 $AB $10 $2F $F0 $6F $E4 $FD $C4 $11 $E4 $FE $C4 $12 $E4
              $FF $C4 $13 $6F $6F $8F $00 $21 $8F $00 $22 $78 $01 $20 $D0 $06
              $3F $A1 $02 $8F $00 $20 $CD $70 $3F $BB $02 $7D $60 $88 $10 $68
              $F0 $5D $90 $F4 $8F $4C $F2 $FA $21 $F3 $8F $5C $F2 $FA $22 $F3
              $6F $CD $78 $8D $00 $F6 $00 $05 $D4 $00 $F6 $01 $05 $D4 $01 $FC
              $FC $7D $60 $88 $10 $68 $F8 $5D $90 $EB $6F $F4 $00 $28 $03 $D8
              $00 $5D $FB $11 $F8 $00 $F4 $00 $28 $80 $D0 $1B $DD $60 $94 $02
              $74 $01 $90 $11 $F4 $03 $1C $60 $88 $08 $84 $00 $3F $E8 $02 $F8
              $00 $B0 $F1 $E8 $00 $D4 $02 $6F $5D $3F $52 $03 $68 $00 $D0 $05
              $3F $5B $03 $2F $5B $68 $01 $D0 $05 $3F $5C $03 $2F $4F $68 $02
              $D0 $05 $3F $64 $03 $2F $47 $68 $03 $D0 $05 $3F $90 $03 $2F $40
              $68 $04 $D0 $05 $3F $A2 $03 $2F $37 $68 $05 $D0 $05 $3F $AA $03
              $2F $2B $68 $06 $D0 $05 $3F $B5 $03 $2F $22 $68 $07 $D0 $05 $3F
              $C9 $03 $2F $19 $68 $08 $D0 $05 $3F $D5 $03 $2F $10 $68 $09 $D0
              $05 $3F $01 $04 $2F $07 $68 $0A $D0 $06 $3F $25 $04 $80 $2F $01
              $60 $6F $E7 $00 $BB $00 $D0 $02 $BB $01 $6F $6F $3F $52 $03 $F8
              $00 $D4 $01 $6F $3F $52 $03 $2D $3F $52 $03 $C4 $01 $F8 $00 $F4
              $00 $28 $FC $04 $01 $D4 $00 $8D $00 $DB $02 $5D $FD $E8 $01 $DC
              $30 $03 $1C $2F $FA $48 $07 $C4 $F1 $AE $D4 $FA $8F $07 $F1 $6F
              $3F $52 $03 $FD $F8 $00 $F4 $00 $28 $40 $D0 $05 $DD $04 $21 $C4
              $21 $6F $3F $52 $03 $04 $22 $C4 $22 $6F $3F $52 $03 $C4 $F2 $3F
              $52 $03 $C4 $F3 $6F $3F $52 $03 $C4 $F2 $BC $FD $3F $52 $03 $C4
              $F3 $CB $F2 $3F $52 $03 $C4 $F3 $6F $3F $52 $03 $FD $3F $52 $03
              $D4 $01 $DB $00 $6F $3F $52 $03 $2D $D8 $01 $F8 $00 $7D $60 $88
              $04 $94 $03 $5D $AE $74 $00 $F0 $0B $30 $09 $BB $00 $F8 $01 $3F
              $C9 $03 $2F $0C $E8 $00 $D4 $00 $F8 $01 $3F $52 $03 $3F $52 $03
              $6F $D8 $01 $F8 $00 $F4 $03 $68 $03 $B0 $11 $BB $03 $F8 $01 $3F
              $52 $03 $FD $3F $52 $03 $D4 $03 $DB $02 $2F $08 $F8 $01 $3F $52
              $03 $3F $52 $03 $6F $F8 $00 $9B $03 $10 $04 $E8 $00 $D4 $03 $6F

              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
              $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00 $00
            }

            ;#File bgm.asm




            ;# ===============================================================//
            ;#                                                                //
            ;# Code                                                           //
            ;#                                                                //
            ;# ===============================================================//
;#{

              ;# =============================================================//
              ;# Variables                                                    //
              ;# =============================================================//
              ;# Scratch pad
              ;#Name $00 b[16] scratch

              ;# System
              ;#Name $10 byte cycles
              ;#Name $11 b[3] counter
              ;#Name $11 byte counter0
              ;#Name $12 byte counter1
              ;#Name $13 byte counter2

              ;# Bgm global
              ;#Name $20 byte bgm.state
              ;#Name $21 byte bgm.kon
              ;#Name $22 byte bgm.kof

              ;# Bgm local
              ;#Name $70 voice[8]
              ;#Name $00 byte voice.flag
              ;#Name $01 byte voice.wait
              ;#Name $02 byte voice.time
              ;#Name $03 byte voice.depth
              ;#Name $04 b[4] voice.loop
              ;#Name $08 w[4] voice.code


              ;# =============================================================//
              ;# Prototypes                                                   //
              ;# =============================================================//
              ;#Code $0200 Reset

              ;#Code $0250 Main
              ;#Code $0266 Sync
              ;#Code $0267 Time

              ;#Code $0274 Sfx

              ;#Code $0275 Bgm
              ;#Code $02A1 Bgm.New
              ;#Code $02BB Bgm.Track
              ;#Code $02E8 Bgm.Parse
              ;#Code $0352 Bgm.Get
              ;#Code $035B Bgm.Pass
              ;#Code $035C Bgm.Wait
              ;#Code $0364 Bgm.Time
              ;#Code $0390 Bgm.Kon
              ;#Code $03A2 Bgm.Kof
              ;#Code $03AA Bgm.Byte
              ;#Code $03B5 Bgm.Word
              ;#Code $03C9 Bgm.Jump
              ;#Code $03D5 Bgm.Loop
              ;#Code $0401 Bgm.Call
              ;#Code $0425 Bgm.Back

              ;#Code $FFC0 Ipl
              ;#Code $FFFE Vector


              ;# =============================================================//
              ;# Reset                                                        //
              ;# =============================================================//
0200          ;Reset:
0200          ;        :                       | // Processor
0200 C0       ;        :DI                     | Di();
0201 20       ;        :CLRP                   | Clrp();
0202          ;        :                       | // Flg
0202 8F 6C F2 ;        :MOV spc.base,#DSP_flg  | spc.base=DSP_flg;
0205 8F E0 F3 ;        :MOV spc.data,#$E0      | spc.data=0xE0;
0208          ;        :                       | // Dsp
0208 E8 00    ;        :MOV A,#$00             | for(X=0;X<0x80;X++){
020A 5D       ;        :MOV X,A                |
020B D8 F2    ;-       :MOV spc.base,X         |   spc.base=X;
020D C4 F3    ;        :MOV spc.data,A         |   spc.data=0;
020F 3D       ;-Flg    :INC X                  |
0210 C8 6C    ;        :CMP X,#$6C             |   if(X+1==DSP_flg) X++;
0212 F0 FB    ;        :BEQ -Flg               |
0214 C8 80    ;        :CMP X,#$80             |
0216 90 F3    ;        :BCC -                  | }
0218          ;        :                       | // Spc
0218 C4 F1    ;        :MOV spc.control,A      | spc.control=0;
021A C4 F4    ;        :MOV spc.port0,A        | spc.port0  =0;
021C C4 F5    ;        :MOV spc.port1,A        | spc.port1  =0;
021E C4 F6    ;        :MOV spc.port2,A        | spc.port2  =0;
0220 C4 F7    ;        :MOV spc.port3,A        | spc.port3  =0;
0222 C4 FA    ;        :MOV spc.timer0,A       | spc.timer0 =0;
0224 C4 FB    ;        :MOV spc.timer1,A       | spc.timer1 =0;
0226 C4 FC    ;        :MOV spc.timer2,A       | spc.timer2 =0;
0228          ;        :                       | // Ram
0228 8F 00 00 ;        :MOV $00,#$00           | $00=0x6000; // End of code
022B 8F 60 01 ;        :MOV $01,#$60           |
022E FD       ;        :MOV Y,A                | Y=0;
022F D7 00    ;-       :MOV [$00]+Y,A          | do{$00[Y]=0;}while(++Y||++$01);
0231 FC       ;        :INC Y                  |
0232 D0 FB    ;        :BNE -                  |
0234 AB 01    ;        :INC $01                |
0236 D0 F7    ;        :BNE -                  |
0238          ;        :                       | // Page 1
0238 5D       ;        :MOV X,A                | X=0;
0239 40       ;        :SETP                   | do{$0100[X]=0;}while(++X);
023A AF       ;-       :MOV (X)+,A             |
023B C8 00    ;        :CMP X,#$00             |
023D D0 FB    ;        :BNE -                  |
023F          ;        :                       | // Page 0
023F 20       ;        :CLRP                   | do{*X=0;}while(++X<0xF0);
0240 AF       ;-       :MOV (X)+,A             |
0241 C8 F0    ;        :CMP X,#$F0             |
0243 90 FB    ;        :BCC -                  |
0245          ;        :                       | // Vector
0245 CD 02    ;        :MOV X,#$02             | $FFFE=Reset;
0247 C9 FF FF ;        :MOV $FFFF,X            |
024A          ;        :                       | // CPU
024A CD FF    ;        :MOV X,#$FF             | SP=0xFF;
024C BD       ;        :MOV SP,X               |
024D 5D       ;        :MOV X,A                | X=0;
024E 60       ;        :CLRC                   | Clrc();
024F E0       ;        :CLRV                   | Clrv();


              ;# =============================================================//
              ;# Main                                                         //
              ;# =============================================================//
0250          ;Main:
0250 8F 07 F1 ;        :MOV spc.control,#$07   | spc.control=0x07;
0253 8F 01 20 ;        :MOV bgm.state,#$01     | bgm.state=0x01;
0256          ;        :                       | for(;;){
0256 3F 66 02 ;-       :CALL Sync              |   Sync();
0259 3F 67 02 ;        :CALL Time              |   Time();
025C 3F 74 02 ;        :CALL Sfx               |   Sfx();
025F 3F 75 02 ;        :CALL Bgm               |   Bgm();
0262 AB 10    ;        :INC cycles             |   cycles++;
0264 2F F0    ;        :BRA -                  | }

0266          ;Sync:
0266          ;        :                       |
0266 6F       ;        :RET                    | return;

0267          ;Time:
0267 E4 FD    ;        :MOV A,spc.counter0     | counter0=spc.counter0;
0269 C4 11    ;        :MOV counter0,A         |
026B E4 FE    ;        :MOV A,spc.counter1     | counter1=spc.counter1;
026D C4 12    ;        :MOV counter1,A         |
026F E4 FF    ;        :MOV A,spc.counter2     | counter2=spc.counter2;
0271 C4 13    ;        :MOV counter2,A         |
0273          ;        :                       |
0273 6F       ;        :RET                    | return;


              ;# =============================================================//
              ;# Sfx                                                          //
              ;# =============================================================//
0274          ;Sfx:
0274          ;        :                       |
0274 6F       ;        :RET                    | return;


              ;# =============================================================//
              ;# Bgm                                                          //
              ;# =============================================================//
0275          ;Bgm:
0275 8F 00 21 ;        :MOV bgm.kon,#$00       | bgm.kon=0x00;
0278 8F 00 22 ;        :MOV bgm.kof,#$00       | bgm.kof=0x00;
              ;        :                       |
027B 78 01 20 ;        :CMP bgm.state,#$01     | if(bgm.state==0x01){
027E D0 06    ;        :BNE +                  |
0280 3F A1 02 ;        :CALL Bgm.New           |   Bgm.New();
0283 8F 00 20 ;        :MOV bgm.state,#$00     |   bgm.state=0x00;
0286          ;+       :                       | }
0286 CD 70    ;        :MOV X,#$70             | for(X=0x70;X<0xF0;X+=0x10){
0288 3F BB 02 ;-       :CALL Track             |   Track(X);
028B 7D       ;        :MOV A,X                |
028C 60       ;        :CLRC                   |
028D 88 10    ;        :ADC A,#$10             |
028F 68 F0    ;        :CMP A,#$F0             |
0291 5D       ;        :MOV X,A                |
0292 90 F4    ;        :BCC -                  | }
              ;        :                       |
0294 8F 4C F2 ;        :MOV spc.base,#$4C      | spc.base=DSP_kon;
0297 FA 21 F3 ;        :MOV spc.data,bgm.kon   | spc.data=bgm.kon;
029A 8F 5C F2 ;        :MOV spc.base,#$5C      | spc.base=DSP_kof;
029D FA 22 F3 ;        :MOV spc.data,bgm.kof   | spc.data=bgm.kof;
              ;        :                       |
02A0 6F       ;        :RET                    | return;

02A1          ;Bgm.New:
02A1 CD 78    ;        :MOV X,#$78             | for(X=0x78;Y=0;X<0xF8;X+=0x10){
02A3 8D 00    ;        :MOV Y,#$00             |
02A5 F6 00 05 ;-       :MOV A,$0500+Y          |   X[0]=$0500[Y];
02A8 D4 00    ;        :MOV $00+X,A            |
02AA F6 01 05 ;        :MOV A,$0501+Y          |   X[1]=$0501[Y];
02AD D4 01    ;        :MOV $01+X,A            |
02AF FC       ;        :INC Y                  |
02B0 FC       ;        :INC Y                  |
02B1 7D       ;        :MOV A,X                |
02B2 60       ;        :CLRC                   |
02B3 88 10    ;        :ADC A,#$10             |
02B5 68 F8    ;        :CMP A,#$F8             |
02B7 5D       ;        :MOV X,A                |
02B8 90 EB    ;        :BCC -                  | }
02BA          ;        :                       |
02BA 6F       ;        :RET                    | return;

02BB          ;Track:
02BB F4 00    ;        :MOV A,voice.flag+X     | Y=counter[voice[X].timer&0x03];
02BD 28 03    ;        :AND A,#$03             |
02BF D8 00    ;        :MOV $00,X              |
02C1 5D       ;        :MOV X,A                |
02C2 FB 11    ;        :MOV Y,counter+X        |
02C4 F8 00    ;        :MOV X,$00              | if(!(voice[X].flag&0x80)){
02C6 F4 00    ;        :MOV A,voice.flag+X     |
02C8 28 80    ;        :AND A,#$80             |
02CA D0 1B    ;        :BNE +Pause             |
02CC DD       ;        :MOV A,Y                |   A=Y+voice[X].time;
02CD 60       ;        :CLRC                   |
02CE 94 02    ;        :ADC A,voice.time+X     |
02D0 74 01    ;        :CMP A,voice.wait+X     |   if(A>=voice[X].wait){
02D2 90 11    ;        :BCC +Wait              |
02D4          ;        :                       |
02D4 F4 03    ;-       :MOV A,voice.depth+X    |     do{
02D6 1C       ;        :ASL A                  |       A=X+8+voice[X].depth*2;
02D7 60       ;        :CLRC                   |
02D8 88 08    ;        :ADC A,#$08             |
02DA 84 00    ;        :ADC A,$00              |
02DC 3F E8 02 ;        :CALL Parse             |     }while(Parse(A));
02DF F8 00    ;        :MOV X,$00              |
02E1 B0 F1    ;        :BCS -                  |
02E3          ;        :                       |
02E3 E8 00    ;        :MOV A,#$00             |     A=0x00;
02E5          ;        :                       |   }
02E5 D4 02    ;+Wait   :MOV voice.time+X,A     |   voice[X].time=A;
02E7          ;        :                       | }
02E7 6F       ;+Pause  :RET                    | return;

02E8          ;Parse:
02E8 5D       ;        :MOV X,A                | X=A;
02E9 3F 52 03 ;        ;CALL Get               | A=Get();
02EC 68 00    ;        :CMP A,#$00             | if(A==0x00){
02EE D0 05    ;        :BNE +                  |
02F0 3F 5B 03 ;        :CALL Pass              |   Pass();
02F3 2F 5B    ;        :BRA +End               |   C=0;
02F5 68 01    ;+       :CMP A,#$01             | }else if(A==0x01){
02F7 D0 05    ;        :BNE +                  |
02F9 3F 5C 03 ;        :CALL Wait              |   Wait();
02FC 2F 4F    ;        :BRA +Next              |   C=1;
02FE 68 02    ;+       :CMP A,#$02             | }else if(A==0x02){
0300 D0 05    ;        :BNE +                  |
0302 3F 64 03 ;        :CALL Time              |   Time();
0305 2F 47    ;        :BRA +Next              |   C=1;
0307 68 03    ;+       :CMP A,#$03             | }else if(A==0x03){
0309 D0 05    ;        :BNE +                  |
030B 3F 90 03 ;        :CALL Kon               |   Kon ();
030E 2F 40    ;        :BRA +End               |   C=0;
0310 68 04    ;+       :CMP A,#$04             | }else if(A==0x04){
0312 D0 05    ;        :BNE +                  |
0314 3F A2 03 ;        :CALL Kof               |   Kof ();
0317 2F 37    ;        :BRA +End               |   C=0;
0319 68 05    ;+       :CMP A,#$05             | }else if(A==0x05){
031B D0 05    ;        :BNE +                  |
031D 3F AA 03 ;        :CALL Byte              |   Byte();
0320 2F 2B    ;        :BRA +Next              |   C=1;
0322 68 06    ;+       :CMP A,#$06             | }else if(A==0x06){
0324 D0 05    ;        :BNE +                  |
0326 3F B5 03 ;        :CALL Word              |   Word();
0329 2F 22    ;        :BRA +Next              |   C=1;
032B 68 07    ;+       :CMP A,#$07             | }else if(A==0x07){
032D D0 05    ;        :BNE +                  |
032F 3F C9 03 ;        :CALL Jump              |   Jump();
0332 2F 19    ;        :BRA +Next              |   C=1;
0334 68 08    ;+       :CMP A,#$08             | }else if(A==0x08){
0336 D0 05    ;        :BNE +                  |
0338 3F D5 03 ;        :CALL Loop              |   Loop();
033B 2F 10    ;        :BRA +Next              |   C=1;
033D 68 09    ;+       :CMP A,#$09             | }else if(A==0x09){
033F D0 05    ;        :BNE +                  |
0341 3F 01 04 ;        :CALL Call              |   Call();
0344 2F 07    ;        :BRA +Next              |   C=1;
0346 68 0A    ;+       :CMP A,#$0A             | }else if(A==0x0A){
0348 D0 06    ;        :BNE +End               |
034A 3F 25 04 ;        :CALL Back              |   Back();
034D 80       ;+Next   :SETC                   |   C=1;
034E 2F 01    ;        :BRA +Return            |
0350 60       ;+End    :CLRC                   | }
0351 6F       ;+Return :RET                    | return C;

0352          ;Get:
0352 E7 00    ;        :MOV A,[$00+X]          | A=*(*X)++;
0354 BB 00    ;        :INC $00+X              |
0356 D0 02    ;        :BNE +                  |
0358 BB 01    ;        :INC $01+X              |
035A          ;+       :                       |
035A 6F       ;        :RET                    | return;

035B          ;Pass:
035B 6F       ;        :RET                    | return;

035C          ;Wait:
035C 3F 52 03 ;        :CALL Get               | voice->wait=Get(X);
035F F8 00    ;        :MOV X,voice            |
0361 D4 01    ;        :MOV wait+X,A           |
0363 6F       ;        :RET                    | return;

0364          ;Time:
0364 3F 52 03 ;        :CALL Get               | A=Get(X);
0367 2D       ;        :PUSH A                 | Push(A);
0368 3F 52 03 ;        :CALL Get               | A=Get(X);
036B C4 01    ;        :MOV $01,A              | voice->flag=voice->flag&0xFC|A;
036D F8 00    ;        :MOV X,voice            |
036F F4 00    ;        :MOV A,flag+X           |
0371 28 FC    ;        :AND A,#$FC             |
0373 04 01    ;        :OR A,$01               |
0375 D4 00    ;        :MOV flag+X,A           |
0377 8D 00    ;        :MOV Y,#$00             | voice->time=0x00;
0379 DB 02    ;        :MOV time+X,Y           |
037B          ;        :                       |
037B 5D       ;        :MOV X,A                | X=A;
037C FD       ;        :MOV Y,A                | for(Y=A,A=0x01;--Y>0;A<<1);
037D E8 01    ;        :MOV A,#$01             |
037F DC       ;-       :DEC Y                  |
0380 30 03    ;        :BMI +                  |
0382 1C       ;        :ASL A                  |
0383 2F FA    ;        :BRA -                  |
0385 48 07    ;+       :EOR A,#$07             | spc.control=A^0x07;
0387 C4 F1    ;        :MOV spc.control,A      |
0389          ;        :                       |
0389 AE       ;        :POP A                  | spc.timer[X]=Pop();
038A D4 FA    ;        :MOV spc.timer0+X,A     |
038C          ;        :                       |
038C 8F 07 F1 ;        :MOV spc.control,#$07   | spc.control=0x07;
038F          ;        :                       |
038F 6F       ;        :RET                    |

0390          ;Kon:
0390 3F 52 03 ;        :CALL Get               | Y=Get(X);
0393 FD       ;        :MOV Y,A                |
0394 F8 00    ;        :MOV X,voice            | if(!(voice->flag&0x40)){
0396 F4 00    ;        :MOV A,flag+X           |
0398 28 40    ;        :AND A,#$40             |
039A D0 05    ;        :BNE +Mute              |
039C DD       ;        :MOV A,Y                |   bgm.kon|=Y;
039D 04 21    ;        :OR A,bgm.kon           |
039F C4 21    ;        :MOV bgm.kon,A          | }
03A1 6F       ;+Mute   :RET                    | return;

03A2          ;Kof:
03A2 3F 52 03 ;        :CALL Get               | bgm,kof|=Get(X);
03A5 04 22    ;        :OR A,bgm.kof           |
03A7 C4 22    ;        :MOV bgm.kof,A          |
03A9 6F       ;        :RET                    | return;

03AA          ;Byte:
03AA 3F 52 03 ;        :CALL Get               | spc.base=Get(X);
03AD C4 F2    ;        :MOV spc.base,A         |
03AF 3F 52 03 ;        :CALL Get               | spc.data=Get(X);
03B2 C4 F3    ;        :MOV spc.data,A         |
03B4 6F       ;        :RET                    | return;

03B5          ;Word:
03B5 3F 52 03 ;        :CALL Get               | A=Get(X);
03B8 C4 F2    ;        :MOV spc.base,A         | spc.base=A;
03BA BC       ;        :INC A                  |
03BB FD       ;        :MOV Y,A                |
03BC 3F 52 03 ;        :CALL Get               | spc.data=Get(X);
03BF C4 F3    ;        :MOV spc.data,A         |
03C1 CB F2    ;        :MOV spc.base,Y         | spc.base=A+1;
03C3 3F 52 03 ;        :CALL Get               | spc.data=Get(X);
03C6 C4 F3    ;        :MOV spc.data,A         |
03C8 6F       ;        :RET                    | return;

03C9          ;Jump:
03C9 3F 52 03 ;        :CALL Get               | Y=Get(X);
03CC FD       ;        :MOV Y,A                |
03CD 3F 52 03 ;        :CALL Get               | A=Get(X);
03D0 D4 01    ;        :MOV $01+X,A            | X[0]=Y;
03D2 DB 00    ;        :MOV $00+X,Y            | X[1]=A;
03D4 6F       ;        :RET                    | return;

03D5          ;Loop:
03D5 3F 52 03 ;        :CALL Get               | A=Get(X);
03D8 2D       ;        :PUSH A                 |
03D9 D8 01    ;        :MOV code,X             | code=X;
03DB F8 00    ;        :MOV X,voice            | X=voice+0x04+voice->depth;
03DD 7D       ;        :MOV A,X                |
03DE 60       ;        :CLRC                   |
03DF 88 04    ;        :ADC A,#$04             |
03E1 94 03    ;        :ADC A,depth+X          |
03E3 5D       ;        :MOV X,A                |
03E4 AE       ;        :POP A                  |
03E5 74 00    ;        :CMP A,$00+X            | if(A>*X){
03E7 F0 0B    ;        :BEQ +NoLoop            |
03E9 30 09    ;        :BMI +NoLoop            |
03EB BB 00    ;        :INC $00+X              |   *X++;
03ED F8 01    ;        :MOV X,code             |   X=code;
03EF 3F C9 03 ;        :CALL Jump              |   Jump();
03F2 2F 0C    ;        :BRA +Return            | }else{
03F4 E8 00    ;+NoLoop :MOV A,#$00             |   *X=0x00;
03F6 D4 00    ;        :MOV $00+X,A            |
03F8 F8 01    ;        :MOV X,code             |   X=code;
03FA 3F 52 03 ;        :CALL Get               |   Get(X);
03FD 3F 52 03 ;        :CALL Get               |   Get(X);
0400          ;        :                       | }
0400 6F       ;+Return :RET                    | return;

0401          ;Call:
0401 D8 01    ;        :MOV code,X             | if(voice->depth<3){
0403 F8 00    ;        :MOV X,voice            |
0405 F4 03    ;        :MOV A,depth+X          |
0407 68 03    ;        :CMP A,#$03             |
0409 B0 11    ;        :BCS +NoCall            |
040B          ;        :                       |
040B BB 03    ;        :INC depth+X            |   voice->depth++;
040D F8 01    ;        :MOV X,code             |
040F 3F 52 03 ;        :CALL Get               |   Y=Get(X);
0412 FD       ;        :MOV Y,A                |
0413 3F 52 03 ;        :CALL Get               |   A=Get(X);
0416 D4 03    ;        :MOV $03+X,A            |   X[0]=Y;
0418 DB 02    ;        :MOV $02+X,Y            |   X[1]=A;
041A 2F 08    ;        :BRA +Call              | }else{
041C          ;        :                       |
041C F8 01    ;+NoCall :MOV X,code             |
041E 3F 52 03 ;        :CALL Get               |   Get(X);
0421 3F 52 03 ;        :CALL Get               |   Get(X);
0424          ;+Call   :                       | }
0424 6F       ;        :RET                    | return;

0425          ;Back:
0425 F8 00    ;        :MOV X,voice            | if(--voice->depth<0x00){
0427 9B 03    ;        :DEC depth+X            |
0429 10 04    ;        :BPL +                  |
042B E8 00    ;        :MOV A,#$00             |   voice->depth=0x00;
042D D4 03    ;        :MOV depth+X,A          |
042F          ;+       :                       | }
042F 6F       ;        :RET                    | return;


              ;# =============================================================//
              ;# Boot                                                         //
              ;# =============================================================//
              ;        :                   | //=====================//
              ;        :                   | // SPC700 IPL Boot Rom //
              ;        :                   | // (c) Nintendo        //
              ;        :                   | //=====================//
FFC0          ;Ipl:
FFC0 CD EF    ;        :MOV X,#$EF         | SP=0xEF;
FFC2 BD       ;        :MOV SP,X           |
FFC3 E8 00    ;        :MOV A,#$00         | for(X=0xEF;X;X--) *X=0x00;
FFC5 C6       ;-Clear  :MOV (X),A          |
FFC6 1D       ;        :DEC X              |
FFC7 D0 FC    ;        :BNE -Clear         |
FFC9 8F AA F4 ;        :MOV port0,#$AA     | port0=0xAA; // Ready
FFCC 8F BB F5 ;        :MOV port1,#$BB     | port1=0xBB; // Ready
FFCF 78 CC F4 ;-Wait   :CMP port0,#$CC     | while(port0!=0xCC); // Wait
FFD2 D0 FB    ;        :BNE -Wait          | goto +;
FFD4 2F 19    ;        :BRA +Base          | for(;;){
FFD6 EB F4    ;-Move   :MOV Y,port0        |   while(Y=port0); // Wait
FFD8 D0 FC    ;        :BNE -Move          |   for(;;){
FFDA 7E F4    ;-Next   :CMP Y,port0        |     if(Y==port0){ // Data
FFDC D0 0B    ;        :BNE +              |
FFDE E4 F5    ;        :MOV A,port1        |       A=port1; // Read
FFE0 CB F4    ;        :MOV port0,Y        |       port0=Y; // Echo
FFE2 D7 00    ;        :MOV [$00]+Y,A      |       $00[Y]=A; // Write
FFE4 FC       ;        :INC Y              |       if(++Y||++$01>=0) continue;
FFE5 D0 F3    ;        :BNE -Next          |     }else{ // Idle
FFE7 AB 01    ;        :INC $01            |       if(Y>port0) continue;
FFE9 10 EF    ;+       :BPL -Next          |     }
FFEB 7E F4    ;        :CMP Y,port0        |     if(Y<port0) break; // End
FFED 10 EB    ;        :BPL -Next          |   }
FFEF BA F6    ;+Base   :MOVW YA,port2      | +:$00=(word)port2; // Address
FFF1 DA 00    ;        :MOVW $00,YA        |
FFF3 BA F4    ;        :MOVW YA,port0      |   X=port1; // Move/jump flag
FFF5 C4 F4    ;        :MOV port0,A        |   port0=port0; // Echo
FFF7 DD       ;        :MOV A,Y            |
FFF8 5D       ;        :MOV X,A            |   if(!X) break;
FFF9 D0 DB    ;        :BNE -Move          | }
FFFB 1F 00 00 ;        :JMP [$0000+X]      | Jmpix(0x0000);

FFFE          ;Vector:
FFFE C0 FF    ;        :Ipl                | // IPL Rom

;#}

            ;# ===============================================================//
            ;#                                                                //
            ;#                                                                //
            ;#                                                                //
            ;# ===============================================================//

